#! /usr/bin/env zsh

# modified version of default `fzf-history-widget` to allow dynamic editing or
# execution of selected history line
# TODO: look at shellcheck results when switching shebang to bash (anything to
# fix in zsh land?)
# ref - https://github.com/junegunn/fzf/issues/477#issuecomment-444053054
local selected num
setopt localoptions noglobsubst noposixbuiltins pipefail 2> /dev/null
selected=( $(fc -l 1 | eval "$(__fzfcmd) +s --tac +m -n2..,.. --tiebreak=index --toggle-sort=ctrl-r --expect=ctrl-e $FZF_CTRL_R_OPTS -q ${(q)LBUFFER}") )
local ret=$?
[ -n "$selected" ] && {
  local accept=0
  [[ $selected[1] = ctrl-e ]] && {
    accept=1
    shift selected
  }
  num=${selected[1]}
  [ -n "$num" ] && {
    zle vi-fetch-history -n $num
    [[ $accept = 0 ]] && zle accept-line
  }
}
zle reset-prompt
return $ret
